.KEY start/s,stop/s
.BRA {
.KET }

; HST Launcher Music Script
; -------------------------
;
; Date: 2017-12-10
; Author: Henrik Noerfjand Stengaard

; Skip end, if hstlauncherdir environment variable doesn't exist
IF NOT EXISTS ENV:hstlauncherdir
  SKIP end
ENDIF

; Skip end, if hstlauncherdir music directory doesn't exist
IF NOT EXISTS "$hstlauncherdir/Music"
  SKIP end
ENDIF

; Skip end, if hstlaunchermusic environment variable doesn't exist
IF NOT EXISTS "ENV:hstlaunchermusic"
  SKIP end
ENDIF

; Start music
IF NOT "{start}" EQ ""
  ; Pick random music file from hst launcher dir music directory
  setenv randommusic `C:filepick "$hstlauncherdir/music/#?"`

  ; Use easymod to play random music file in background, if any exists
  IF NOT $randommusic EQ ""
    run >NIL: C:easymod >NIL: "$randommusic"
  ENDIF
ENDIF

; Stop music
IF NOT "{stop}" EQ ""
  ; List running processes
  status >T:_stop1

  ; Find "easymod" processes
  search T:_stop1 "easymod" NONUM >T:_stop2

  ; Skip to end, if "easymod" was not found
  If WARN
    SKIP end
  EndIf

  ; Create script to stop easymod processes
  sed "s/^Process/break/" T:_stop2 >T:_stop3
  sed "s/\([0-9]*\):/\1;/" T:_stop3 >T:_stop4

  ; Execute and delete stop easymod processes script
  execute T:_stop4
  delete >NIL: T:_stop#?
ENDIF

; End
LAB end