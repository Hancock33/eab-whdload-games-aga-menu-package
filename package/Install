; EAB WHDLoad Games AGA Menu Package Install Script
; -------------------------------------------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2017-03-09
;
; This script extracts and installs zip archives for EAB WHDLoad Games AGA Menu Package.


echo "Installing Menu and WHDLoad drawers..."
unzip -qq -o -x PACKAGEDIR:menu-whdload-drawers.zip -d WHDLOADDIR:

echo "Installing AGS2 Games Menu..."
unzip -qq -o -x PACKAGEDIR:ags2games-menu.zip -d WHDLOADDIR:Menu/AGS2Games/
protect >NIL: WHDLOADDIR:Menu/AGS2Games/AGS2 +rwe
protect >NIL: WHDLOADDIR:Menu/AGS2Games/AGS2Helper +rwe
protect >NIL: WHDLOADDIR:Menu/AGS2Games/AGS2Menu +rwe
IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Games/Music"
  makedir "WHDLOADDIR:Menu/AGS2Games/Music" >NIL:
ENDIF
IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Games/.Favourites.ags"
  IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Games/.Favourites.ag_"
    makedir >NIL: "WHDLOADDIR:Menu/AGS2Games/.Favourites.ag_"
  ENDIF
ENDIF

echo "Installing Menu WHDLoad Content..."
unzip -qq -o -x PACKAGEDIR:menu-whdload-content.zip -d WHDLOADDIR:WHDLoad/Games/

; Create systemdir games directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Games
  makedir >NIL: SYSTEMDIR:Games
ENDIF

echo "Installing AGS2 menu support files..."
unzip -qq -o -x PACKAGEDIR:ags2menu-support-files.zip -d SYSTEMDIR:

; Add icon for configuration drawer, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Programs/Configuration.info
  rename >NIL: SYSTEMDIR:Programs/Configuration.info.new SYSTEMDIR:Programs/Configuration.info
ELSE
  delete >NIL: SYSTEMDIR:Programs/Configuration.info.new
ENDIF

; Create default folder icon for systemdir games and programs
IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
  IF NOT EXISTS SYSTEMDIR:Games.info
    copy SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info SYSTEMDIR:Games.info
  ENDIF
  IF NOT EXISTS SYSTEMDIR:Programs.info
    copy SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info SYSTEMDIR:Programs.info
  ENDIF
ELSE
  IF EXISTS SYSTEMDIR:System.info
    IF NOT EXISTS SYSTEMDIR:Games.info
      copy SYSTEMDIR:System.info SYSTEMDIR:Games.info
    ENDIF
    IF NOT EXISTS SYSTEMDIR:Programs.info
      copy SYSTEMDIR:System.info SYSTEMDIR:Programs.info
    ENDIF
  ENDIF
ENDIF

; Fix read write execute flags for ags2 menu support files
protect >NIL: SYSTEMDIR:C/Aren +rwe
protect >NIL: SYSTEMDIR:C/basename +rwe
protect >NIL: SYSTEMDIR:C/cut +rwe
protect >NIL: SYSTEMDIR:C/dirname +rwe
protect >NIL: SYSTEMDIR:C/EasyMOD +rwe
protect >NIL: SYSTEMDIR:C/filepick +rwe
protect >NIL: SYSTEMDIR:C/FRSort +rwe
protect >NIL: SYSTEMDIR:C/FSearch +rwe
protect >NIL: SYSTEMDIR:C/GetLines +rwe
protect >NIL: SYSTEMDIR:C/Rep +rwe
protect >NIL: SYSTEMDIR:C/ReqList +rwe
protect >NIL: SYSTEMDIR:C/RequestString +rwe
protect >NIL: SYSTEMDIR:C/sed +rwe
protect >NIL: SYSTEMDIR:C/tail +rwe
protect >NIL: SYSTEMDIR:C/tr +rwe
protect >NIL: SYSTEMDIR:C/wc +rwe

; Create settings directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:S/Settings
  makedir >NIL: SYSTEMDIR:S/Settings
ENDIF

; Create default whdloadargs for ags2 menus, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:S/Settings/whdloadargs
  echo "Preload" > SYSTEMDIR:S/Settings/whdloadargs
ENDIF


; Configure ClassicWB
; -------------------

; Skip, if assign startup doesn't exist
IF NOT EXISTS SYSTEMDIR:S/Assign-Startup
  SKIP hstwb
ENDIF

; Skip, if assign startup doesn't contain "ClassicWB"
search SYSTEMDIR:S/Assign-Startup "ClassicWB" >NIL:
IF WARN
  SKIP hstwb
ENDIF

echo "Configuring ClassicWB..."

; Update default A-Games assign
rep SYSTEMDIR:S/Assign-Startup "SYS:Games" "DH1:WHDLoad/Games"

; Add HD-Games to assign startup, if it doesn't exist
search SYSTEMDIR:S/Assign-Startup "HD-Games:" >NIL:
IF WARN 
	echo "Assign >NIL: HD-Games: DH1:HD-Games" >>SYSTEMDIR:S/Assign-Startup
ENDIF

; Add ags2 startup to user startup, if it doesn't exist
IF EXISTS SYSTEMDIR:S/User-Startup
  search SYSTEMDIR:S/User-Startup "S:AGS2Startup" >NIL:
  IF WARN 
    echo "IF EXISTS S:AGS2Startup" >>SYSTEMDIR:S/User-Startup
    echo "  execute S:AGS2Startup" >>SYSTEMDIR:S/User-Startup
    echo "ENDIF" >>SYSTEMDIR:S/User-Startup
  ENDIF
ENDIF

; Delete systemdir games directory, if "ags2 games" file doesn't exist
IF NOT EXISTS "SYSTEMDIR:Games/AGS2 Games"
  delete >NIL: SYSTEMDIR:Games ALL
  makedir >NIL: SYSTEMDIR:Games
ENDIF

SKIP configuremenus


; Configure HstWB
; ---------------
LAB hstwb

; Skip, if user assign doesn't exist
IF EXISTS SYSTEMDIR:S/User-Assign
  echo "Configuring HstWB..."
ELSE
  echo "Configuring Workbench..."
	echo "" NOLINE >SYSTEMDIR:S/User-Assign
ENDIF

; Add execute user assign, if it doesn't exist
search SYSTEMDIR:S/Startup-Sequence "execute S:User-Assign" >NIL:
IF WARN 
  rep SYSTEMDIR:S/Startup-Sequence "BindDrivers" "execute S:User-Assign*N*NBindDrivers"
ENDIF

; Add A-Games to user-assign, if it doesn't exist
search SYSTEMDIR:S/User-Assign "A-Games:" >NIL:
IF WARN 
	echo "Assign >NIL: A-Games: DH1:WHDLoad/Games" >>SYSTEMDIR:S/User-Assign
ENDIF

; Add HD-Games to user assign, if it doesn't exist
search SYSTEMDIR:S/User-Assign "HD-Games:" >NIL:
IF WARN 
	echo "Assign >NIL: HD-Games: DH1:HD-Games" >>SYSTEMDIR:S/User-Assign
ENDIF


; Configure menus
; ------------------------
LAB configuremenus

; Add ags2 games menu dir to ags2menu.data, if it doesn't exist
search SYSTEMDIR:S/AGS2Menu.data "AGS2GamesMenuDir=" >NIL:
IF WARN 
	echo "AGS2GamesMenuDir=DH1:Menu/AGS2Games" >>SYSTEMDIR:S/AGS2Menu.data
ENDIF

; Add igame games menu dir to igamemenu.data, if it doesn't exist
search SYSTEMDIR:S/iGameMenu.data "iGameGamesMenuDir=" >NIL:
IF WARN 
	echo "iGameGamesMenuDir=DH0:Games" >>SYSTEMDIR:S/iGameMenu.data
ENDIF

echo "Installing HstWB Games..."
unzip -qq -o -x PACKAGEDIR:hstwb-games.zip -d SYSTEMDIR:Games/


; End
LAB end