; EAB WHDLoad Games AGA Menu Package Install Script
; -------------------------------------------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2017-12-10
;
; This script extracts and installs zip archives for EAB WHDLoad Games AGA Menu Package.


; Copy readme and screenshots to locale help for EAB WHDLoad Games AGA Menu
IF NOT EXISTS SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-AGA-Menu
  makepath >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-AGA-Menu
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-AGA-Menu FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-AGA-Menu
  ENDIF
ENDIF
IF NOT EXISTS SYSTEMDIR:Locale/Help.info
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale/Help FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale/Help
  ENDIF
ENDIF
IF NOT EXISTS SYSTEMDIR:Locale.info
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale
  ENDIF
ENDIF
copy >NIL: PACKAGEDIR:README SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-AGA-Menu
copy >NIL: PACKAGEDIR:README.guide SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-AGA-Menu
copy >NIL: PACKAGEDIR:README.info SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-AGA-Menu
IF NOT EXISTS SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-AGA-Menu/screenshots
  makepath >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-AGA-Menu/screenshots
ENDIF
copy >NIL: PACKAGEDIR:screenshots/#?.iff SYSTEMDIR:Locale/Help/EAB-WHDLoad-Games-AGA-Menu/screenshots


; Create systemdir games directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Games
  makedir >NIL: SYSTEMDIR:Games
ENDIF

echo "Installing frontends..."
unzip -qq -o -x PACKAGEDIR:frontends.zip -d SYSTEMDIR:Games/


echo "Installing data drawers..."
unzip -qq -o -x PACKAGEDIR:data-drawers.zip -d WHDLOADDIR:


echo "Installing menus..."
IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Games"
  makedir "WHDLOADDIR:Menu/AGS2Games" >NIL:
ENDIF
unzip -qq -o -x PACKAGEDIR:ags2.zip -d WHDLOADDIR:Menu/AGS2Games/
IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Games/Music"
  makedir "WHDLOADDIR:Menu/AGS2Games/Music" >NIL:
ENDIF
IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Games/.Favourites.ags"
  IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Games/.Favourites.ag_"
    makedir >NIL: "WHDLOADDIR:Menu/AGS2Games/.Favourites.ag_"
  ENDIF
ENDIF

IF NOT EXISTS "WHDLOADDIR:Menu/HSTGames"
  makedir "WHDLOADDIR:Menu/HSTGames" >NIL:
ENDIF
unzip -qq -o -x PACKAGEDIR:hst.zip -d WHDLOADDIR:Menu/HSTGames/


; Install set
set installset `RequestChoice "Games AGA menu set" "Select Games AGA menu set to install." "All|4GB"`

IF $installset EQ 1 VAL
  unzip -qq -o -x PACKAGEDIR:data-games.zip -d WHDLOADDIR:WHDLoad/Games/
  unzip -qq -o -x PACKAGEDIR:ags2-games.zip -d WHDLOADDIR:Menu/AGS2Games/
  unzip -qq -o -x PACKAGEDIR:hst-games.zip -d WHDLOADDIR:Menu/HSTGames/
  unzip -qq -o -x PACKAGEDIR:frontends-games.zip -d SYSTEMDIR:Games/
ENDIF

IF $installset EQ 2 VAL
  unzip -qq -o -x PACKAGEDIR:data-games4gb.zip -d WHDLOADDIR:WHDLoad/Games/
  unzip -qq -o -x PACKAGEDIR:ags2-games4gb.zip -d WHDLOADDIR:Menu/AGS2Games/
  unzip -qq -o -x PACKAGEDIR:hst-games4gb.zip -d WHDLOADDIR:Menu/HSTGames/
  unzip -qq -o -x PACKAGEDIR:frontends-games4gb.zip -d SYSTEMDIR:Games/
ENDIF

echo "Installing system files..."
unzip -qq -o -x PACKAGEDIR:system-files.zip -d SYSTEMDIR:

; Add icon for configuration drawer, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Programs/Configuration.info
  rename >NIL: SYSTEMDIR:Programs/Configuration.info.new SYSTEMDIR:Programs/Configuration.info
ELSE
  delete >NIL: SYSTEMDIR:Programs/Configuration.info.new
ENDIF

; Create default folder icon for systemdir games and programs
IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
  IF NOT EXISTS SYSTEMDIR:Games.info
    copy SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info SYSTEMDIR:Games.info
  ENDIF
  IF NOT EXISTS SYSTEMDIR:Programs.info
    copy SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info SYSTEMDIR:Programs.info
  ENDIF
ELSE
  IF EXISTS SYSTEMDIR:System.info
    IF NOT EXISTS SYSTEMDIR:Games.info
      copy SYSTEMDIR:System.info SYSTEMDIR:Games.info
    ENDIF
    IF NOT EXISTS SYSTEMDIR:Programs.info
      copy SYSTEMDIR:System.info SYSTEMDIR:Programs.info
    ENDIF
  ENDIF
ENDIF

; Create systemdir whdload prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/WHDLoad
  MakePath >NIL: SYSTEMDIR:Prefs/WHDLoad
ENDIF

; Create default whdloadargs, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/WHDLoad/whdloadargs
  echo "Preload" > SYSTEMDIR:Prefs/WHDLoad/whdloadargs
ENDIF

; Create systemdir ags2 prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/AGS2
  MakePath >NIL: SYSTEMDIR:Prefs/AGS2
ENDIF

; Create ags2music prefs to turn on music in ags2 menu
IF NOT EXISTS SYSTEMDIR:Prefs/AGS2/ags2music
  echo "" NOLINE > SYSTEMDIR:Prefs/AGS2/ags2music
ENDIF

; Create systemdir hst launcher prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/HSTLauncher
  MakePath >NIL: SYSTEMDIR:Prefs/HSTLauncher
ENDIF

; Create hstlaunchermusic prefs to turn on music in hst launcher menu
IF NOT EXISTS SYSTEMDIR:Prefs/HSTLauncher/hstlaunchermusic
  echo "" NOLINE > SYSTEMDIR:Prefs/HSTLauncher/hstlaunchermusic
ENDIF

; Create systemdir igame prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/iGame
  MakePath >NIL: SYSTEMDIR:Prefs/iGame
ENDIF

; Add menu startup to startup sequence, if it doesn't exist
search SYSTEMDIR:S/Startup-Sequence "execute S:Menu-Startup" >NIL:
IF WARN 
  rep SYSTEMDIR:S/Startup-Sequence "BindDrivers" "IF EXISTS S:Menu-Startup*N  execute S:Menu-Startup*NENDIF*N*NBindDrivers"
ENDIF


; Configure ClassicWB
; -------------------

set gamesdir "`execute INSTALLDIR:S/CombinePath "$WHDLOADDIR" "WHDLoad/Games"`"

; Skip, if assign startup doesn't exist
IF NOT EXISTS SYSTEMDIR:S/Assign-Startup
  SKIP hstwb
ENDIF

; Skip, if assign startup doesn't contain "ClassicWB"
search SYSTEMDIR:S/Assign-Startup "ClassicWB" >NIL:
IF WARN
  SKIP hstwb
ENDIF

echo "Configuring ClassicWB..."

; Add or update A-Games to user-assign
search SYSTEMDIR:S/Assign-Startup "A-Games:" >NIL:
IF WARN 
	echo "Assign >NIL: A-Games: $gamesdir" >>SYSTEMDIR:S/Assign-Startup
ELSE
  setenv updateassign `RequestChoice "Update assign" "A-Games: assign already exists and must be updated to*Npath '$gamesdir'.*N*NNote: Updating assign requires a reboot after*Ninstallation is done!*N*NDo you want to update A-Games: assign?" "Yes|No"`
  IF "$updateassign" EQ "1"
    echo "$gamesdir" >T:_gamesdir
    set gamesdir "`sed "s/\//\\\//g" T:_gamesdir`"
    sed "s/\([Aa]-[Gg][Aa][Mm][Ee][Ss]:\)/\1 $gamesdir;/" "SYSTEMDIR:S/Assign-Startup" >"T:Assign-Startup"
    copy >NIL: "T:Assign-Startup" "SYSTEMDIR:S/Assign-Startup"
  ENDIF   
ENDIF

SKIP configuremenus


; Configure HstWB
; ---------------
LAB hstwb

; Skip, if user assign doesn't exist
IF EXISTS SYSTEMDIR:S/User-Assign
  echo "Configuring HstWB..."
ELSE
  echo "Configuring Workbench..."
	echo "" NOLINE >SYSTEMDIR:S/User-Assign
ENDIF

; Add execute user assign to startup sequence, if it doesn't exist
search SYSTEMDIR:S/Startup-Sequence "execute S:User-Assign" >NIL:
IF WARN 
  rep SYSTEMDIR:S/Startup-Sequence "BindDrivers" "IF EXISTS S:User-Assign*N  execute S:User-Assign*NENDIF*N*NBindDrivers"
ENDIF

; Add or update A-Games to user-assign
search SYSTEMDIR:S/User-Assign "A-Games:" >NIL:
IF WARN 
	echo "Assign >NIL: A-Games: $gamesdir" >>SYSTEMDIR:S/User-Assign
ELSE
  setenv updateassign `RequestChoice "Update assign" "A-Games: assign already exists and must be updated to*Npath '$gamesdir'.*N*NNote: Updating assign requires a reboot after*Ninstallation is done!*N*NDo you want to update A-Games: assign?" "Yes|No"`
  IF "$updateassign" EQ "1"
    echo "$gamesdir" >T:_gamesdir
    set gamesdir "`sed "s/\//\\\//g" T:_gamesdir`"
    sed "s/\([Aa]-[Gg][Aa][Mm][Ee][Ss]:\)/\1 $gamesdir;/" "SYSTEMDIR:S/User-Assign" >"T:User-Assign"
    copy >NIL: "T:User-Assign" "SYSTEMDIR:S/User-Assign"
  ENDIF
ENDIF


; Configure menus
; ------------------------
LAB configuremenus

echo "Configuring menus..."

; Add or update ags2 games menu dir to AGS2Menus.cfg
set ags2gamesdir "`execute INSTALLDIR:S/CombinePath "$WHDLOADDIR" "Menu/AGS2Games"`"
search SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg "AGS2GamesDir=" >NIL:
IF WARN 
	echo "AGS2GamesDir=$ags2gamesdir" >>SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg
ELSE
  rep SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg "AGS2GamesDir=" "AGS2GamesDir=$ags2gamesdir*N; "
ENDIF

; Add or update igame games menu dir to iGameMenus.cfg
set igamegamesdir "`execute INSTALLDIR:S/CombinePath "$SYSTEMDIR" "Games"`"
search SYSTEMDIR:Prefs/iGame/iGameMenus.cfg "iGameGamesDir=" >NIL:
IF WARN 
	echo "iGameGamesDir=$igamegamesdir" >>SYSTEMDIR:Prefs/iGame/iGameMenus.cfg
ELSE
  rep SYSTEMDIR:Prefs/iGame/iGameMenus.cfg "iGameGamesDir=" "iGameGamesDir=$igamegamesdir*N; "
ENDIF

; Add or update hst games dir to HSTLauncherMenus.cfg
set hstgamesdir "`execute INSTALLDIR:S/CombinePath "$WHDLOADDIR" "Menu/HSTGames"`"
search SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "iGameGamesDir=" >NIL:
IF WARN 
	echo "iGameGamesDir=$hstgamesdir" >>SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg
ELSE
  rep SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "iGameGamesDir=" "iGameGamesDir=$hstgamesdir*N; "
ENDIF

; End
LAB end